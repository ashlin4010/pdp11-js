; Javascript PDP 11/70 Emulator v1.9
; written by Paul Nankervis
; Please send suggestions, fixes and feedback to paulnank@hotmail.com
;
; boot loader code
;rebase higher link/bot:140000 boot

    .macro lda label
    mov pc,r0
    add label-.,r0
    .endm

start:
    reset
    clr @#177776
    lda #start
    mov r0,sp

    lda #banner
    jsr pc,print        ;print banner

    sub #256,sp         ;make room for cmd buffer
    mov sp,buffer

    jsr pc,getcmd       ;print prompt, start clock, read input...

    clr r0
20$: wait
    inc r0
    br 20$              ;idle loop

getcmd:
    lda #prompt
    jsr pc,print        ;print cmd prompt

    mov buffer,bufptr   ;reset input buffer

    lda #inpast
    mov r0,@#60
    mov #200,@#62
    movb #100,@#177560  ;set console input handler

    lda #clkast
    mov r0,@#100
    mov #340,@#102
    movb #100,@#177546  ;ensure clock is ticking
    rts pc

clkcnt: .word 0
clkast:
    dec clkcnt
    mov clkcnt,@#177570 ;tickle display register
    rtt

putchr:
    tstb @#177564
    bpl putchr
    movb r0,@#177566    ;write a character to console terminal
    rts pc

prtptr: .word 0
print:
    bitb #100,@#177564
    bne print           ;wait until terminal ie is not set
    mov r0,prtptr
    lda #prtast
    mov r0,@#64
    mov #200,@#66
    mov #100,@#177564   ;set print in motion
    rts pc

prtast:
    tstb @prtptr        ;check for end of string
    beq 20$
    movb @prtptr,@#177566
    inc prtptr
    rtt
20$:clrb @#177564      ;turn off ie
    rtt

buffer: .word 0 ;input buffer location
bufptr: .word 0 ;input buffer pointer

inpast:
    mov r0,-(sp)
    mov r1,-(sp)
    movb @#177562,r0    ;get character
    cmpb r0,#40
    blt 30$             ;below space?
    cmpb r0,#127.
    bge 50$             ;above del? go test equality
    mov bufptr,r1
    sub buffer,r1
    cmp r1,#250
    bgt 90$             ;no room in buffer?
    jsr pc,putchr       ;echo the char
    cmpb r0,#97.
    blt 10$             ;less than lower a
    cmpb r0,#122.
    bgt 10$             ;greater than lower z
    bicb #40,r0         ;convert to upper case
10$:mov bufptr,r1
    movb r0,(r1)+       ;put character in buffer
    mov r1,bufptr
    br  90$

30$:cmpb r0,#15
    beq 80$             ;for carriage return do a command
    cmpb r0,#10
50$:bne 90$             ;is it <del> or back space
    cmp bufptr,buffer   ;any data to remove?
    blos 90$
    dec bufptr
    lda #del            ;backspace to delete character
    jsr pc,print
    br 90$

80$:mov bufptr,r0
    clrb (r0)
    lda #cmd
    mov r0,@#240
    mov #40,@#242
    mov #1000,@#177772  ;call cmd at priority 1 using pir

90$:mov (sp)+,r1        ;restore registers & exit
    mov (sp)+,r0
    rtt

cmd:
    clr @#177772        ; no more pir calls
    mov r0,-(sp)
    mov r1,-(sp)
    mov r2,-(sp)
    mov r3,-(sp)
    mov r4,-(sp)
    mov r5,-(sp)

    lda #eol
    jsr pc,print

    mov buffer,r5       ;check cmd in buffer (it is uppercase only!)
20$:movb (r5)+,r4
    beq 90$             ;done if end
    cmp r4,#40
    beq 20$             ;skip spaces

    clr r3              ;command index
    lda #cmdlst         ;command names

30$:movb (r0)+,r1       ;get first command table byte
    beq 80$             ;end so unknown cmd
    cmpb r1,r4
    bne 50$             ;if no match next command

    mov r5,r2
40$:movb (r2),r1        ;look at other command bytes
    beq  70$
    inc r2
    cmp r1,#40
    beq 70$
    tstb (r0)
    beq  50$
    cmpb r1,(r0)+       ;compare input byte with table
    beq 40$

50$:tstb (r0)+          ;find next table command
    bne 50$
    add #2,r3
    br 30$

70$:bitb #100,@#177564
    bne 70$             ;wait for printing before executing
    lda #cmdtbl         ;execute command routine at r3 offset
    add r0,r3
    add (r3),r3
    jsr pc,(r3)
    br 90$

80$:lda #unkmsg         ;unknown command
    jsr pc,print

90$:jsr pc,getcmd       ;print prompt, and ensure everything is running

    mov (sp)+,r5
    mov (sp)+,r4
    mov (sp)+,r3
    mov (sp)+,r2
    mov (sp)+,r1
    mov (sp)+,r0
    rtt

cmdtbl: .word boot-.,help-.,lights-.,chaser-.,odt-.
banner: .asciz 'Paul Nankervis - paulnank@hotmail.com'<15><12><15><12>
prompt: .asciz 'Boot> '
hlpmsg: .ascii 'Commands are BOOT, HELP, LIGHTS, CHASER, ODT'<15><12>
        .ascii 'Boot devices are RK RL RP TM or PR'<15><12>
        .asciz 'eg. Boot> BOOT RP1'<15><12><15><12>
eol:    .byte 15,12,0
del:    .byte 10,40,10,0;
cmdlst: .asciz 'BOOT'
        .asciz 'HELP'
        .asciz 'LIGHTS'
        .asciz 'CHASER'
        .asciz 'ODT'
        .byte 0
unkmsg: .asciz 'Valid commands are BOOT, HELP, LIGHTS, CHASER, ODT'<12><15>
badboo: .asciz 'Valid boot devices are RK RL RP TM and PR eg rk0'<12><15>

        .even

help:
    lda #hlpmsg         ;help message
    jsr pc,print
    rts pc

chaser:
    mov #1,r0
    rol r0
    reset               ;loop forever chasing the light
    br .-4

mmr0=177572
mmr1=177574
mmr2=177576
mmr3=172516

lights:
    clr @#mmr0
    clr r1
    mov #172300,r2      ;kernel map
    jsr pc,95$
    mov #177600,@#172376 ;kernel d has i/o space
    clr r1
    mov #172200,r2      ;super map
    jsr pc,95$
    mov #177600,@#172276 ;super d has i/o space
    clr r1
    mov #177600,r2      ;user map
    jsr pc,95$
    mov #177600,@#177676 ;user d has i/o space
    mov #27,@#mmr3      ;set mapping mode - 22 bit split i/d
    mov #1,@#mmr0       ;enable map

    mov #7000,r0        ;physical address for super code (upper mem somewhere)
    mov r0,@#172244     ;base super i (par 2) #40000
    mov r0,@#172264     ;base super d (par 2) #40000
    bic #030000,@#177776
    bis #010000,@#177776 ;set pm to super (don't change priority!)
    mov #40200,r3       ;virtual address to load code
    lda #supers         ;where we get code
    mov r0,r2
    lda #supere         ;end of code
10$:mov (r2)+,-(sp)
    mtpi (r3)+          ;write to super space
    cmp r2,r0
    blo 10$

    mov #40200,-(sp)
    mtpi sp              ;set super sp
    mov #40200,@#240
    mov #054040,@#242
    mov #1000,@#177772  ;call #40200 in super at priority 1 using pir
    rts pc

95$:mov #8.,r0          ;subroutine to set up mapping pdr/par r2 map, r1 base
99$:mov r1,60(r2)       ;load d par
    mov r1,40(r2)       ;load i par
    mov #77406,20(r2)   ;load d pdr
    mov #77406,(r2)+    ;load i pdr
    add #200,r1
    sob r0,99$
    rts pc

;this code is copied to super virtual address #40200 (par 2)
;virtual address #40000 to #40101 for wait & jmp instructions (written using par 2)
supers:
    clr @#177772        ;turn off pir
    bic #340,@#177776   ;lower priority
    mov #37,r0          ;load initial data pattern
    mov #174000,r1      ;load initial address pattern

10$:mov r1,r2
    sub #2,r2
    bic #1,r2           ;compute virtual address for wait

    mov r2,r3
    bic #177701,r3      ;address offset
    mov #0000001,40000(r3)  ; write wait instruction
    mov #0000113,40002(r3)  ; write jmp (r3) instruction

    mov r2,r4
    ash #-6,r4
    bic #177600,r4
    mov @#172244,r3
    sub r4,r3           ;par address base for wait

    mov r2,r4
    ash #-12.,r4
    bic #177761,r4      ;par select offset - wait execution uses computed par
    mov r3,172240(r4)   ;super i space

    mov r1,r5           ;virtual address for jmp
    ash #-12.,r5
    bic #177761,r5      ;par select offset - jmp execution uses appropriate par
    cmp r5,r4           ;same par as wait?
    beq 30$
    mov r3,172240(r5)   ;super i space

30$:mov pc,r3
    add #45$-.,r3       ;generate jmp return address
    mov #3,r4           ;repeat count
40$:jmp (r2)            ;jmp to wait
45$:sob r4,40$

    bit #1,@#177570     ;change rotation on switch bit zero
    bne 50$
    mov r0,r2
    ror r2              ;rotate pattern anticlockwise
    rol r1
    ror r0
    br 10$
50$:mov r0,r2
    rol r2              ;rotate pattern clockwise
    ror r1
    rol r0
    br 10$
supere:                 ;end of code for super mode

bootbl: .word bootrk-.,bootrl-.,bootrp-.,boottm-.,bootpt-.
boolst: .ascii 'RKRLRPTMPR'
        .word 0
boount: .word 5, 3, 4, 3, 0
boot:
10$:movb (r2)+,r4
    cmpb r4,#40
    beq 10$             ;ignore leading spaces
    tstb r4
    beq 80$             ;must have a char
    movb (r2)+,r0
    beq 80$             ;must have two
    swab r0
    bis r0,r4           ;combine two characters (ie rk, rl..)

    clr r3              ;unit
20$:movb (r2)+,r0
    beq 30$
    sub #'0,r0
    cmpb r0,#7
    bhi 80$
    ash #3,r3
    bisb r0,r3          ;get any unit number digits
    br 20$

30$:clr r2              ;name index
    lda #boolst
40$:mov (r0)+,r1        ;get bootname
    beq 80$
    cmp r1,r4           ;match?
    beq 50$
    add #2,r2
    br 40$

50$:lda #boount
    add r2,r0
    cmp r3,(r0)
    bgt 80$             ;unit out of range

    lda #bootbl         ;execute command routine at r2 offset
    add r0,r2
    add (r2),r2
    jsr pc,(r2)
    br 90$

80$:lda #badboo         ;complain we don't know boot device
    jsr pc,print

90$:rts pc

rlcs=174400
bootrl:
    reset
    swab r3         ; unit number
    mov #rlcs,r0    ; csr
    mov #13,4(r0)   ; seek
    bis #4,r3       ; unit+gstat
    mov r3,(r0)     ; issue cmd
    tstb (r0)       ; wait
    bpl .-2
    clr 2(r0)       ; clr ba
    clr 4(r0)       ; clr da
    mov #-512.,6(r0) ; set wc
    clrb r3
    bis #14,r3      ; unit+read
    mov r3,(r0)     ; issue cmd
    tstb (r0)       ; wait
    bpl .-2
    clr pc

rkda=177412
bootrk:
    reset
    mov #rkda,r0    ; csr
    ash #13.,r3     ; unit # into da
    mov r3,(r0)     ; load da
    clr -(r0)       ; clear ba
    mov #-256.*2,-(r0) ; load wc
    mov #5,-(r0)    ; read & go
    tstb (r0)
    bpl .-2
    clr pc

rpcsr=0176700
bootrp:
    reset
    mov #rpcsr, r1
    mov #40, 10(r1)      ; reset
    mov r3, 10(r1)       ; set unit
    mov #0000021, (r1)   ; pack ack
    mov #0010000, 32(r1) ; 16b mode
    mov #-512., 2(r1)    ; set wc
    clr 4(r1)            ; clr ba
    clr 6(r1)            ; clr da
    clr 34(r1)           ; clr cyl
    mov #0000071, (r1)   ; read
    tstb (r1)            ; wait
    bpl .-2
    clrb (r1)
    mov r3,r0            ;required for BSD
    clr pc


mtcma=0172526
boottm:
    reset
    mov r3, r0          ; unit
    mov #mtcma, r1      ; mtcma
    clr (r1)
    mov #-1, -(r1)      ; mtbrc
    mov r0,r2
    swab r2
    add #60011, r2
    mov r2, -(r1)       ; space + go
    tstb (r1)           ; mtc
    bpl .-2
    mov r0,r2
    swab r2
    add #60003, r2
    mov r2, (r1)        ; read + go
    tstb (r1)           ; mtc
    bpl .-2
    clr pc

;Paper tape boot routine using RT11 LDA format
;Input format:
;Frame
;  1    001
;  2    000
;  3    Low order byte count    (Includes all except checksum,
;  4    High order byte count       even the 001 000.)
;  5    Low order load address
;  6    High order load address
;  7..  Data bytes
;  XXX  Checksum    (Includes all the block - even the 001)
;
;The checksum is calculated such that when all the bytes have
;been added up, the low byte of the sum will be zero.
;
;If the byte count is 6, the load address specified will be
;taken to be the start address of the program. If the address
;is even the program will be started, otherwise ODT will be
;re-entered.  If (count > 6), the data block will be loaded.
;
;This is the format used by DEC for paper tapes, and can be
;produced using the RT-11 LINK/LDA command.

;Register use:
; R0 - input byte       R1 - checksum
; R2 - load address     R3 - byte count
; R4 - start address flag

ptrcsr = 177550 ;Address of reader device

pterr:.asciz 'Paper tape checksum error'<15><12>
ptnost:.asciz 'Paper tape has no start address'<15><12>
.even

bootpt:
    reset               ;Required as some code overlays our vectors
10$:clr r1              ;Initialize checksum
    jsr pc,getch
    cmpb r0,#1          ;Read until start of frame
    bne 10$
    jsr pc,getch
    tstb r0             ;Byte after start must be zero
    bne 10$

    jsr pc,getwd
    mov r2,r3           ;Get byte count in R3
    jsr pc,getwd        ;Get load address in R2
    add @#177570,r2     ;Add switches for relocation

    sub #6,r3
    mov r3,r4           ;Flag whether there is data
    ble 40$             ;If no data then must be a start address

30$:jsr pc,getch
    movb r0,(r2)+       ;Put byte into memory
    sob r3,30$

40$:jsr pc,getch
    tstb r1             ;Verify checksum
    bne chkerr

    tst r4
    bgt 10$             ;If that frame contained data go get more

    tst r2
    beq nostrt          ;If load address null then don't start
    bit #1,r2
    bne nostrt          ;If load address odd then don't start

    jmp (r2)            ;See what happens

getwd:                  ;Get a word from reader in R2
    jsr pc,getch
    mov r0,r2           ;Low order
    jsr pc,getch
    swab r0
    bis r0,r2           ;Hi order
    rts pc

getch:                  ;Get a character from reader in R0
    mov #ptrcsr,r0      ;Reader device
    mov #1,(r0)         ;Start reader
1$: tstb (r0)
    bpl 1$              ;Wait for a character
    movb 2(r0),r0       ;Get it
    bic #177400,r0
    add r0,r1           ;Accumulate checksum
    rts pc

chkerr:
    lda #pterr
    br ptend

nostrt:
    lda #ptnost
ptend:
    jsr pc,print
    rts pc

end: .end start
